pkgname=nerd-fonts-meslo
_name="Meslo"
pkgver=2.1.0
pkgrel=1
pkgdesc="A Nerd Font patched version of Meslo"
arch=('any')
url="https://github.com/ryanoasis/nerd-fonts"
license=('MIT')
depends=('fontconfig')
provides=('nerd-fonts-meslo')
makedepends=('fontconfig' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('nerd-fonts-git' 'nerd-fonts' 'nerd-fonts-complete')
install="$pkgname.install"
source=("$_name-$pkgver.zip::https://github.com/ryanoasis/nerd-fonts/releases/download/v$pkgver/$_name.zip"
        "https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/LICENSE")
sha256sums=('f0630f93b2f8c27b0cda8c4a2bae2b7a67bdd70786500e109f38c3a9b145f523'
            'd2a29823384e9194a87936ccad495c764c2ef733b29bfa6f72a1d65803ce02e5')

package() {
	install -d {"$pkgdir/usr/share/fonts/OTF","$pkgdir/usr/share/fonts/TTF"}
	find . -iname "*.otf" -not -iname "*Windows Compatible.otf" \
		-execdir install -m644 {} "$pkgdir/usr/share/fonts/OTF/{}" \;

	find . -iname "*.ttf" -not -iname "*Windows Compatible.ttf" \
		-execdir install -m644 {} "$pkgdir/usr/share/fonts/TTF/{}" \;

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
